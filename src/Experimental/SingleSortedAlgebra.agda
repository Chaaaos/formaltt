open import Agda.Primitive
open import Agda.Builtin.Nat
open import Data.Fin

open import Experimental.Equality

open import Categories.Category
open import Categories.Category.Cartesian

module Experimental.SingleSortedAlgebra where

-- an algebraic signature
record Signature : Setâ‚ where
  field
    oper : Set -- operations
    oper-arity : oper â†’ Nat -- the arity of an operation

open Signature

Context = Nat
var = Fin

-- terms over a signature in a context of a given sort
data Term (Î£ : Signature) (Î“ : Context) : Set where
  tm-var : âˆ€ (x : var Î“) â†’ Term Î£ Î“
  tm-op : âˆ€ (f : oper Î£) â†’ (âˆ€ (i : Fin (oper-arity Î£ f)) â†’ Term Î£ Î“) â†’ Term Î£ Î“

substitution : âˆ€ (Î£ : Signature) (Î“ Î” : Context) â†’ Set
substitution Î£ Î“ Î” = var Î“ â†’ Term Î£ Î”

-- identity substitution
id-substitution : âˆ€ {Î£ : Signature} {Î“ : Context} â†’ substitution Î£ Î“ Î“
id-substitution = tm-var

-- the action of a substitution on a term
_Â·_ : âˆ€ {Î£ : Signature} {Î“ Î” : Context} â†’ substitution Î£ Î“ Î” â†’ Term Î£ Î“ â†’ Term Î£ Î”
Ïƒ Â· (tm-var x) = Ïƒ x
Ïƒ Â· (tm-op f x) = tm-op f (Î» i â†’ Ïƒ Â· x i)

infixr 6 _Â·_

-- composition of substitutions
_âˆ˜s_ : âˆ€ {Î£ : Signature} {Î“ Î” Î˜ : Context} â†’ substitution Î£ Î” Î˜ â†’ substitution Î£ Î“ Î” â†’ substitution Î£ Î“ Î˜
(Ïƒ âˆ˜s Ï„) x = Ïƒ Â· Ï„ x

infixl 7 _âˆ˜s_

-- an equational theory is a family of equations over a given sort
record Theory â„“ (Î£ : Signature) : Set (lsuc â„“) where
  field
    eq : Set â„“
    eq-ctx : âˆ€ (Îµ : eq) â†’ Context
    eq-lhs : âˆ€ (Îµ : eq) â†’ Term Î£ (eq-ctx Îµ)
    eq-rhs : âˆ€ (Îµ : eq) â†’ Term Î£ (eq-ctx Îµ)

  infix 4 _âŠ¢_â‰ˆ_

  -- equality of terms
  data _âŠ¢_â‰ˆ_ : (Î“ : Context) â†’ Term Î£ Î“ â†’ Term Î£ Î“ â†’ Set (lsuc â„“) where
    -- general rules
    eq-refl : âˆ€ {Î“} {t : Term Î£ Î“} â†’ Î“ âŠ¢ t â‰ˆ t
    eq-symm : âˆ€ {Î“} {s t : Term Î£ Î“} â†’ Î“ âŠ¢ s â‰ˆ t â†’ Î“ âŠ¢ t â‰ˆ s
    eq-tran : âˆ€ {Î“} {s t u : Term Î£ Î“} â†’ Î“ âŠ¢ s â‰ˆ t â†’ Î“ âŠ¢ t â‰ˆ u â†’ Î“ âŠ¢ s â‰ˆ u
    -- congruence rule
    eq-congr : âˆ€ {Î“} {f : oper Î£} (x y : âˆ€ (i : Fin (oper-arity Î£ f)) â†’ Term Î£ Î“) â†’
               (âˆ€ i â†’ Î“ âŠ¢ x i â‰ˆ y i) â†’ Î“ âŠ¢ tm-op f x â‰ˆ tm-op f y
    -- equational axiom
    eq-axiom : âˆ€ (Îµ : eq) {Î” : Context} (Ïƒ : substitution Î£ (eq-ctx Îµ) Î”) â†’
               Î” âŠ¢ Ïƒ Â· (eq-lhs Îµ) â‰ˆ Ïƒ Â· eq-rhs Îµ

  -- equality of substitutions
  _â‰ˆs_ : âˆ€ {Î“ Î” : Context} â†’ substitution Î£ Î“ Î” â†’ substitution Î£ Î“ Î” â†’ Set (lsuc â„“)
  _â‰ˆs_ {Î” = Î”} Ïƒ Ï = âˆ€ x â†’ Î” âŠ¢ Ïƒ x â‰ˆ Ï x

  -- composition of substitutions is functorial
  subst-âˆ˜s : âˆ€ {Î“ Î” Î˜} (Ïƒ : substitution Î£ Î” Î˜) (Ï„ : substitution Î£ Î“ Î”) â†’ âˆ€ (t : Term Î£ Î“) â†’ Î˜ âŠ¢ (Ïƒ Â· Ï„ Â· t) â‰ˆ (Ïƒ âˆ˜s Ï„ Â· t)
  subst-âˆ˜s Ïƒ Ï„ (tm-var x) = eq-refl
  subst-âˆ˜s Ïƒ Ï„ (tm-op f x) = eq-congr (Î» i â†’ Ïƒ Â· Ï„ Â· x i) (Î» i â†’ Ïƒ âˆ˜s Ï„ Â· x i) Î» i â†’ subst-âˆ˜s Ïƒ Ï„ (x i)

  -- substitution preserves equality
  eq-subst : âˆ€ {Î“ Î” : Context} (Ïƒ : substitution Î£ Î“ Î”) {s t : Term Î£ Î“} â†’ Î“ âŠ¢ s â‰ˆ t â†’ Î” âŠ¢ Ïƒ Â· s â‰ˆ Ïƒ Â· t
  eq-subst Ïƒ eq-refl = eq-refl
  eq-subst Ïƒ (eq-symm Î¾) = eq-symm (eq-subst Ïƒ Î¾)
  eq-subst Ïƒ (eq-tran Î¶ Î¾) = eq-tran (eq-subst Ïƒ Î¶) (eq-subst Ïƒ Î¾)
  eq-subst Ïƒ (eq-congr x y Î¾) = eq-congr (Î» i â†’ Ïƒ Â· x i) (Î» i â†’ Ïƒ Â· y i) Î» i â†’ eq-subst Ïƒ (Î¾ i)
  eq-subst Ïƒ (eq-axiom Îµ Ï„) =
    eq-tran (subst-âˆ˜s Ïƒ Ï„ (eq-lhs Îµ))
            (eq-tran (eq-axiom Îµ (Ïƒ âˆ˜s Ï„)) (eq-symm (subst-âˆ˜s Ïƒ Ï„ (eq-rhs Îµ))))

module _ {â„“t o â„“ e : Level}
         (Î£ : Signature) (T : Theory â„“t Î£) (ğ’ : Category o â„“ e)
         (cartesian-ğ’ : Cartesian ğ’) where
  open Category ğ’
  open import Categories.Object.Product ğ’
  open Cartesian cartesian-ğ’
  open HomReasoning

  -- We use our own definition of powers (because the one in the library has a silly special case n = 1
  pow : âˆ€ (A : Obj) (n : Nat) â†’ Obj
  pow A zero = âŠ¤
  pow A (suc n) = pow A n Ã— A

  pow-Ï€ : âˆ€ {A : Obj} {n : Nat} (i : Fin n) â†’ pow A n â‡’ A
  pow-Ï€ {_} {suc n} zero = Ï€â‚‚
  pow-Ï€ {_} {suc n} (suc i) = (pow-Ï€ i) âˆ˜ Ï€â‚

  pow-tuple : âˆ€ {A B : Obj} {n : Nat} â†’ (Fin n â†’ A â‡’ B) â†’ A â‡’ pow B n
  pow-tuple {n = zero} fs = !
  pow-tuple {n = suc n} fs = âŸ¨ (pow-tuple (Î» i â†’ fs (suc i))) , (fs zero) âŸ©

  pow-tuple-âˆ˜ : âˆ€ {A B C : Obj} {n : Nat} {fs : Fin n â†’ B â‡’ C} {g : A â‡’ B} â†’
                pow-tuple (Î» i â†’ fs i âˆ˜ g) â‰ˆ pow-tuple fs âˆ˜ g
  pow-tuple-âˆ˜ {n = zero} {fs} {g} = !-unique (! âˆ˜ g)
  pow-tuple-âˆ˜ {n = suc n} {fs = fs} =
    let open product in
      (âŸ¨âŸ©-congÊ³ (pow-tuple-âˆ˜ {fs = Î» i â†’ fs (suc i)})) â—‹ (âŸº âŸ¨âŸ©âˆ˜)

  pow-tuple-id : âˆ€ {A : Obj} {n} â†’ pow-tuple {A = pow A n} {n = n} pow-Ï€ â‰ˆ id
  pow-tuple-id {n = zero} = !-unique id
  pow-tuple-id {n = suc n} = (âŸ¨âŸ©-congÊ³ ((pow-tuple-âˆ˜ {n = n}) â—‹ ((pow-tuple-id {n = n} âŸ©âˆ˜âŸ¨refl) â—‹ identityË¡))) â—‹ Î·

  -- An interpretation of Î£ in ğ’
  record Interp : Set (o âŠ” â„“ âŠ” e) where
    field
      interp-carrier : Obj
      interp-oper : âˆ€ (f : oper Î£) â†’ pow interp-carrier (oper-arity Î£ f) â‡’ interp-carrier

    -- the interpretation of a term
    interp-term : âˆ€ {Î“ : Context} â†’ Term Î£ Î“ â†’  ğ’ [ (pow interp-carrier Î“) , interp-carrier ]
    interp-term (tm-var x) = pow-Ï€ x
    interp-term (tm-op f ts) = ğ’ [ interp-oper f âˆ˜ pow-tuple (Î» i â†’ interp-term (ts i)) ]

  -- Every signature has the trivial interpretation

  TrivialInterp : Interp
  TrivialInterp = record { interp-carrier = âŠ¤ ; interp-oper = Î» f â†’ ! }

  record Hom (A B : Interp) : Set (o âŠ” â„“ âŠ” e) where
    open Interp

    field
      hom-morphism : interp-carrier A  â‡’ interp-carrier B
      hom-commute :
         âˆ€ (f : oper Î£) â†’
         hom-morphism âˆ˜ interp-oper A f â‰ˆ
             interp-oper B f âˆ˜ pow-tuple {n = oper-arity Î£ f} (Î» i â†’  hom-morphism âˆ˜  interp-oper A f)

  -- The identity homomorphism
  Id : âˆ€ (A : Interp) â†’ Hom A A
  Id A = record
          { hom-morphism = id
          ; hom-commute = {!!}
          }

  -- Compositon of homomorphisms
  _âˆ˜I_ : âˆ€ {A B C : Interp} â†’ Hom B C â†’ Hom A B â†’ Hom A C
  Ï• âˆ˜I Ïˆ =
    let open Hom in
    record { hom-morphism = (hom-morphism Ï•) âˆ˜ (hom-morphism Ïˆ)
           ; hom-commute = {!!} }

  -- Model of a theory
  record Mod : Set (â„“t âŠ” o âŠ” â„“ âŠ” e) where
    open Interp
    open Theory

    field
      {{model-interp}} : Interp
      model-eq : âˆ€ (Îµ : eq T) â†’ interp-term model-interp (eq-lhs T Îµ) â‰ˆ interp-term model-interp (eq-rhs T Îµ)

  -- Every theory has the trivial model, whose carrier is the terminal object

  TrivialModel : Mod
  TrivialModel =
    record
      { model-interp = TrivialInterp
      ; model-eq = Î» Îµ â†’ !-uniqueâ‚‚
      }

  -- Syntactic category
  SynCat : Category lzero lzero (lsuc â„“t)
  SynCat =
    let open Theory in
      record
        { Obj = Context
        ; _â‡’_ = substitution Î£
        ; _â‰ˆ_ = _â‰ˆs_ T
        ; id =  id-substitution
        ; _âˆ˜_ =  _âˆ˜s_
        ; assoc = {!!}
        ; sym-assoc = {!!}
        ; identityË¡ = {!!}
        ; identityÊ³ = {!!}
        ; identityÂ² = {!!}
        ; equiv = {!!}
        ; âˆ˜-resp-â‰ˆ = {!!}
        }
