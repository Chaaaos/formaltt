open import Level
open import Relation.Binary.PropositionalEquality
open import Relation.Binary using (Setoid)
import Function.Equality

import Categories.Category
import Categories.Functor
import Categories.Category.Instance.Setoids

import Categories.Category.Cocartesian

import SecondOrder.Arity
import SecondOrder.Signature
import SecondOrder.Metavariable
import SecondOrder.Term

module SecondOrder.Renaming
  {â„“}
  {ğ”¸ : SecondOrder.Arity.Arity}
  (Î£ : SecondOrder.Signature.Signature â„“ ğ”¸)
  where

  open SecondOrder.Signature.Signature Î£
  open SecondOrder.Metavariable Î£
  open SecondOrder.Term Î£

  -- a renaming maps variables between contexts in a type-preserving way
  _â‡’Ê³_ : âˆ€ (Î“ Î” : Context) â†’ Set â„“
  Î“ â‡’Ê³ Î” = âˆ€ {A} â†’ A âˆˆ Î“ â†’ A âˆˆ Î”

  infix 4 _â‡’Ê³_

  -- equality of renamings

  _â‰¡Ê³_ : âˆ€ {Î“ Î”} (Ïƒ Ï„ : Î“ â‡’Ê³ Î”) â†’ Set â„“
  _â‰¡Ê³_ {Î“} Ïƒ Ï„ = âˆ€ {A} (x : A âˆˆ Î“) â†’ Ïƒ x â‰¡ Ï„ x

  infixl 3 _â‰¡Ê³_

  â‰¡Ê³-refl : âˆ€ {Î“ Î”} {Ï : Î“ â‡’Ê³ Î”} â†’ Ï â‰¡Ê³ Ï
  â‰¡Ê³-refl = Î» x â†’ refl

  â‰¡Ê³-sym : âˆ€ {Î“ Î”} {Ï Î½ : Î“ â‡’Ê³ Î”}
          â†’ Ï â‰¡Ê³ Î½
          â†’ Î½ â‰¡Ê³ Ï
  â‰¡Ê³-sym eq x = sym (eq x)

  â‰¡Ê³-trans : âˆ€ {Î“ Î”} {Ï Î½ Î³ : Î“ â‡’Ê³ Î”}
           â†’ Ï â‰¡Ê³ Î½
           â†’ Î½ â‰¡Ê³ Î³
           â†’ Ï â‰¡Ê³ Î³
  â‰¡Ê³-trans eq1 eq2 x = trans (eq1 x) (eq2 x)

  -- renamings form a setoid

  renaming-setoid : âˆ€ (Î“ Î” : Context) â†’ Setoid â„“ â„“
  renaming-setoid Î“ Î” =
    record
      { Carrier = Î“ â‡’Ê³ Î”
      ;  _â‰ˆ_ = Î» Ï Î½ â†’ Ï â‰¡Ê³ Î½
      ; isEquivalence =
                      record
                        { refl = Î» {Ï} x â†’ â‰¡Ê³-refl {Î“} {Î”} {Ï} x
                        ; sym = â‰¡Ê³-sym
                        ; trans = â‰¡Ê³-trans
                        }
      }

  -- the identity renaming

  idÊ³ : âˆ€ {Î“ : Context} â†’ Î“ â‡’Ê³ Î“
  idÊ³ x = x

  -- composition of renamings
  _âˆ˜Ê³_ : âˆ€ {Î“ Î” Î} â†’ Î” â‡’Ê³ Î â†’ Î“ â‡’Ê³ Î” â†’ Î“ â‡’Ê³ Î
  (Ïƒ âˆ˜Ê³ Ï) x = Ïƒ (Ï x)

  infix 7 _âˆ˜Ê³_

  -- composition respects equality
  âˆ˜Ê³-resp-â‰¡Ê³ : âˆ€ {Î“ Î” Î} {Ï„â‚ Ï„â‚‚ : Î” â‡’Ê³ Î} {Ïƒâ‚ Ïƒâ‚‚ : Î“ â‡’Ê³ Î”} â†’
                 Ï„â‚ â‰¡Ê³ Ï„â‚‚ â†’ Ïƒâ‚ â‰¡Ê³ Ïƒâ‚‚ â†’ Ï„â‚ âˆ˜Ê³ Ïƒâ‚ â‰¡Ê³ Ï„â‚‚ âˆ˜Ê³ Ïƒâ‚‚
  âˆ˜Ê³-resp-â‰¡Ê³ {Ï„â‚ = Ï„â‚} {Ïƒâ‚‚ = Ïƒâ‚‚} Î¶ Î¾ x = trans (cong Ï„â‚ (Î¾ x)) (Î¶ (Ïƒâ‚‚ x))

  -- the identity is the unit

  identity-leftÊ³ : âˆ€ {Î“ Î”} {Ï : Î“ â‡’Ê³ Î”} â†’ idÊ³ âˆ˜Ê³ Ï â‰¡Ê³ Ï
  identity-leftÊ³ Ï = refl

  identity-rightÊ³ : âˆ€ {Î“ Î”} {Ï : Î“ â‡’Ê³ Î”} â†’ Ï âˆ˜Ê³ idÊ³ â‰¡Ê³ Ï
  identity-rightÊ³ Ï = refl

  -- composition is associative

  assocÊ³ : âˆ€ {Î“ Î” Î Î¨} {Ï„ : Î“ â‡’Ê³ Î”} {Ï : Î” â‡’Ê³ Î} {Ïƒ : Î â‡’Ê³ Î¨} â†’
             (Ïƒ âˆ˜Ê³ Ï) âˆ˜Ê³ Ï„ â‰¡Ê³ Ïƒ âˆ˜Ê³ (Ï âˆ˜Ê³ Ï„)
  assocÊ³ x = refl

  sym-assocÊ³ : âˆ€ {Î“ Î” Î Î¨} {Ï„ : Î“ â‡’Ê³ Î”} {Ï : Î” â‡’Ê³ Î} {Ïƒ : Î â‡’Ê³ Î¨} â†’
             Ïƒ âˆ˜Ê³ (Ï âˆ˜Ê³ Ï„) â‰¡Ê³ (Ïƒ âˆ˜Ê³ Ï) âˆ˜Ê³ Ï„
  sym-assocÊ³ x = refl

  -- contexts and renamings form a category
  module _ where
    open Categories.Category

    Contexts : Category â„“ â„“ â„“
    Contexts =
      record
        { Obj = Context
        ; _â‡’_ = _â‡’Ê³_
        ; _â‰ˆ_ = _â‰¡Ê³_
        ; id = idÊ³
        ; _âˆ˜_ = _âˆ˜Ê³_
        ; assoc = Î» {_} {_} {_} {_} {f} {g} {h} {_} â†’ assocÊ³ {Ï„ = f} {Ï = g} {Ïƒ = h}
        ; sym-assoc = Î» {_} {_} {_} {_} {f} {g} {h} {_} â†’ sym-assocÊ³ {Ï„ = f} {Ï = g} {Ïƒ = h}
        ; identityË¡ = Î» x â†’ refl
        ; identityÊ³ = Î» x â†’ refl
        ; identityÂ² = Î» x â†’ refl
        ; equiv = record { refl = Î» {Ï} {_} â†’ â‰¡Ê³-refl {Ï = Ï} ; sym = â‰¡Ê³-sym ; trans = â‰¡Ê³-trans }
        ; âˆ˜-resp-â‰ˆ = âˆ˜Ê³-resp-â‰¡Ê³
        }


  -- the coproduct structure of the category
  module _ where

    infixl 7 [_,_]Ê³

    [_,_]Ê³ : âˆ€ {Î“ Î” Î} â†’ Î“ â‡’Ê³ Î â†’ Î” â‡’Ê³ Î â†’ Î“ ,, Î” â‡’Ê³ Î
    [ Ïƒ , Ï„ ]Ê³ (var-inl x) = Ïƒ x
    [ Ïƒ , Ï„ ]Ê³ (var-inr y) = Ï„ y

    inlÊ³ : âˆ€ {Î“ Î”} â†’ Î“ â‡’Ê³ Î“ ,, Î”
    inlÊ³ = var-inl

    inrÊ³ : âˆ€ {Î“ Î”} â†’ Î” â‡’Ê³ Î“ ,, Î”
    inrÊ³ = var-inr

    uniqueÊ³ : âˆ€ {Î“ Î” Î} {Ï„ : Î“ ,, Î” â‡’Ê³ Î} {Ï : Î“ â‡’Ê³ Î} {Ïƒ : Î” â‡’Ê³ Î}
              â†’ Ï„ âˆ˜Ê³ inlÊ³ â‰¡Ê³ Ï
              â†’ Ï„ âˆ˜Ê³ inrÊ³ â‰¡Ê³ Ïƒ
              â†’ [ Ï , Ïƒ ]Ê³ â‰¡Ê³ Ï„
    uniqueÊ³ Î¾ Î¶ (var-inl x) = sym (Î¾ x)
    uniqueÊ³ Î¾ Î¶ (var-inr y) = sym (Î¶ y)

    Context-+ : Categories.Category.Cocartesian.BinaryCoproducts Contexts
    Context-+ =
      record {
        coproduct =
          Î» {Î“ Î”} â†’
          record
            { A+B = Î“ ,, Î”
            ; iâ‚ = inlÊ³
            ; iâ‚‚ = inrÊ³
            ; [_,_] = [_,_]Ê³
            ; injectâ‚ = Î» x â†’ refl
            ; injectâ‚‚ = Î» x â†’ refl
            ; unique = uniqueÊ³
            }
      }

  open Categories.Category.Cocartesian.BinaryCoproducts Context-+

  -- extension of a renaming is summing with identity
  â‡‘Ê³ : âˆ€ {Î“ Î” Î} â†’ Î“ â‡’Ê³ Î” â†’ Î“ ,, Î â‡’Ê³ Î” ,, Î
  â‡‘Ê³ Ï = Ï +â‚ idÊ³

  -- a renaming can also be extended on the right
  Ê³â‡‘Ê³ : âˆ€ {Î“ Î”} â†’ Î“ â‡’Ê³ Î” â†’ âˆ€ {Î} â†’ Î ,, Î“ â‡’Ê³ Î ,, Î”
  Ê³â‡‘Ê³ Ï = idÊ³ +â‚ Ï


  -- the action of a renaming on terms
  module _ {Î˜ : MetaContext} where

    infix 6 [_]Ê³_

    [_]Ê³_ : âˆ€ {Î“ Î” A} â†’ Î“ â‡’Ê³ Î” â†’ Term Î˜ Î“ A â†’ Term Î˜ Î” A
    [ Ï ]Ê³ (tm-var x) = tm-var (Ï x)
    [ Ï ]Ê³ (tm-meta M ts) = tm-meta M (Î» i â†’ [ Ï ]Ê³ (ts i))
    [ Ï ]Ê³ (tm-oper f es) = tm-oper f (Î» i â†’ [ â‡‘Ê³ Ï ]Ê³ (es i))

  -- the reassociation renaming
  rename-assoc : âˆ€ {Î“ Î” Î} â†’ Î“ ,, (Î” ,, Î) â‡’Ê³ (Î“ ,, Î”) ,, Î
  rename-assoc (var-inl x) = var-inl (var-inl x)
  rename-assoc (var-inr (var-inl y)) = var-inl (var-inr y)
  rename-assoc (var-inr (var-inr z)) = var-inr z

  -- the inverse of the reassociation renaming

  rename-unassoc : âˆ€ {Î“ Î” Î} â†’ (Î“ ,, Î”) ,, Î â‡’Ê³ Î“ ,, (Î” ,, Î)
  rename-unassoc (var-inl (var-inl x)) = var-inl x
  rename-unassoc (var-inl (var-inr x)) = var-inr (var-inl x)
  rename-unassoc (var-inr x) = var-inr (var-inr x)

  -- apply the reassociation renaming on terms
  term-reassoc : âˆ€ {Î˜ Î” Î“ Î A}
    â†’ Term Î˜ (Î” ,, (Î“ ,, Î)) A
    â†’ Term Î˜ ((Î” ,, Î“) ,, Î) A
  term-reassoc = [ rename-assoc ]Ê³_

  -- the empty context is the right unit

  ctx-empty-right-unit : âˆ€ {Î“} â†’ Î“ ,, ctx-empty â‡’Ê³ Î“
  ctx-empty-right-unit (var-inl x) = x

  rename-ctx-empty-inv : âˆ€ {Î“} â†’ Î“ â‡’Ê³ Î“ ,, ctx-empty
  rename-ctx-empty-inv x = var-inl x

  -- the empty context is the left unit

  ctx-empty-left-unit : âˆ€ {Î“} â†’ ctx-empty ,, Î“ â‡’Ê³ Î“
  ctx-empty-left-unit (var-inr x) = x

  -- The sum of identities is an identity
  idÊ³+idÊ³ : âˆ€ {Î“ Î”} â†’ idÊ³ {Î“ = Î“} +â‚ idÊ³ {Î“ = Î”} â‰¡Ê³ idÊ³ {Î“ = Î“ ,, Î”}
  idÊ³+idÊ³ (var-inl x) = refl
  idÊ³+idÊ³ (var-inr y) = refl

  -- The action of a renaming respects equality of terms
  []Ê³-resp-â‰ˆ : âˆ€ {Î˜ Î“ Î” A} {s t : Term Î˜ Î“ A} {Ï : Î“ â‡’Ê³ Î”} â†’ s â‰ˆ t â†’ [ Ï ]Ê³ s â‰ˆ [ Ï ]Ê³ t
  []Ê³-resp-â‰ˆ (â‰ˆ-â‰¡ refl) = â‰ˆ-â‰¡ refl
  []Ê³-resp-â‰ˆ (â‰ˆ-meta Î¾) = â‰ˆ-meta (Î» i â†’ []Ê³-resp-â‰ˆ (Î¾ i))
  []Ê³-resp-â‰ˆ (â‰ˆ-oper Î¾) = â‰ˆ-oper (Î» i â†’ []Ê³-resp-â‰ˆ (Î¾ i))

  -- The action of a renaming respects equality of renamings
  []Ê³-resp-â‰¡Ê³ : âˆ€ {Î˜ Î“ Î” A} {Ï Ï„ : Î“ â‡’Ê³ Î”} {t : Term Î˜ Î“ A} â†’ Ï â‰¡Ê³ Ï„ â†’ [ Ï ]Ê³ t â‰ˆ [ Ï„ ]Ê³ t
  []Ê³-resp-â‰¡Ê³ {t = tm-var x} Î¾ = â‰ˆ-â‰¡ (cong tm-var (Î¾ x))
  []Ê³-resp-â‰¡Ê³ {t = tm-meta M ts} Î¾ = â‰ˆ-meta (Î» i â†’ []Ê³-resp-â‰¡Ê³ Î¾)
  []Ê³-resp-â‰¡Ê³ {t = tm-oper f es} Î¾ = â‰ˆ-oper (Î» i â†’ []Ê³-resp-â‰¡Ê³ (+â‚-congâ‚‚ Î¾ â‰¡Ê³-refl))

  -- The action of the identity is trival
  [id]Ê³ : âˆ€ {Î˜ Î“ A} {t : Term Î˜ Î“ A} â†’ [ idÊ³ ]Ê³ t â‰ˆ t
  [id]Ê³ {t = tm-var x} = â‰ˆ-refl
  [id]Ê³ {t = tm-meta M ts} = â‰ˆ-meta Î» i â†’ [id]Ê³
  [id]Ê³ {t = tm-oper f es} = â‰ˆ-oper Î» i â†’ â‰ˆ-trans ([]Ê³-resp-â‰¡Ê³ idÊ³+idÊ³) [id]Ê³

  -- Extension respects composition
  â‡‘Ê³-âˆ˜Ê³ : âˆ€ {Î“ Î” Î Î¨} {Ï : Î“ â‡’Ê³ Î”} {Ï„ : Î” â‡’Ê³ Î} â†’ â‡‘Ê³ {Î = Î¨} (Ï„ âˆ˜Ê³ Ï) â‰¡Ê³ (â‡‘Ê³ Ï„) âˆ˜Ê³ (â‡‘Ê³ Ï)
  â‡‘Ê³-âˆ˜Ê³ (var-inl x) = refl
  â‡‘Ê³-âˆ˜Ê³ (var-inr y) = refl

  -- The action of a renaming is functorial
  [âˆ˜]Ê³ : âˆ€ {Î˜ Î“ Î” Î} {Ï : Î“ â‡’Ê³ Î”} {Ï„ : Î” â‡’Ê³ Î} {A} {t : Term Î˜ Î“ A} â†’ [ Ï„ âˆ˜Ê³ Ï ]Ê³ t â‰ˆ [ Ï„ ]Ê³ ([ Ï ]Ê³ t)
  [âˆ˜]Ê³ {t = tm-var x} = â‰ˆ-refl
  [âˆ˜]Ê³ {t = tm-meta M ts} = â‰ˆ-meta (Î» i â†’ [âˆ˜]Ê³)
  [âˆ˜]Ê³ {t = tm-oper f es} = â‰ˆ-oper (Î» i â†’ â‰ˆ-trans ([]Ê³-resp-â‰¡Ê³ â‡‘Ê³-âˆ˜Ê³) [âˆ˜]Ê³)

  -- Forming terms over a given metacontext and sort is functorial in the context
  module _ {Î˜ : MetaContext} {A : sort} where
    open Categories.Functor
    open Categories.Category.Instance.Setoids

    Term-Functor : Functor Contexts (Setoids â„“ â„“)
    Term-Functor =
      record
        { Fâ‚€ = Î» Î“ â†’ Term-setoid Î˜ Î“ A
        ; Fâ‚ = Î» Ï â†’ record { _âŸ¨$âŸ©_ = [ Ï ]Ê³_ ; cong = []Ê³-resp-â‰ˆ }
        ; identity = â‰ˆ-trans [id]Ê³
        ; homomorphism = Î» Î¾ â†’ â‰ˆ-trans ([]Ê³-resp-â‰ˆ Î¾) [âˆ˜]Ê³
        ; F-resp-â‰ˆ = Î» Î¶ Î¾ â†’ â‰ˆ-trans ([]Ê³-resp-â‰¡Ê³ Î¶) ([]Ê³-resp-â‰ˆ Î¾)
        }

  -- interactions between "reassociation" and "unassociation"
  -- (the functions that change the way the concatenation of context is associated)
  -- the reassociation renaming and "unassociation" renaming are inverse
  unassoc-reassoc : âˆ€ {Î“ Î” Î} â†’ (rename-unassoc {Î”} {Î“} {Î}) âˆ˜Ê³ rename-assoc â‰¡Ê³ idÊ³
  unassoc-reassoc (var-inl x) = refl
  unassoc-reassoc (var-inr (var-inl x)) = refl
  unassoc-reassoc (var-inr (var-inr x)) = refl

  -- "reassociating" and then "unassociating" a term acts like the identity
  unassoc-reassoc-tm : âˆ€ {Î˜ Î“ Î” Î A} (t : Term Î˜ (Î“ ,, (Î” ,, Î)) A) â†’ [ rename-unassoc ]Ê³ (term-reassoc t) â‰ˆ t
  unassoc-reassoc-tm t = â‰ˆ-trans
                           (â‰ˆ-trans
                             (â‰ˆ-sym ([âˆ˜]Ê³))
                             ([]Ê³-resp-â‰¡Ê³ unassoc-reassoc))
                           ([id]Ê³)

  -- term-reassociation preserves syntactical equality of terms
  â‰ˆ-tm-reassoc : âˆ€ {Î˜ Î“ Î” Î A} {s t : Term Î˜ (Î“ ,, (Î” ,, Î)) A}
                 â†’ term-reassoc s â‰ˆ term-reassoc t â†’ s â‰ˆ t
  â‰ˆ-tm-reassoc {s = s} {t = t} p = â‰ˆ-trans
                                     (â‰ˆ-sym (unassoc-reassoc-tm s))
                                     (â‰ˆ-sym (â‰ˆ-trans
                                       (â‰ˆ-sym (unassoc-reassoc-tm t))
                                       ([]Ê³-resp-â‰ˆ (â‰ˆ-sym p))))

  -- -- extending two times is like extending one time and unassociating
  extendÊ³Â² : âˆ€ {Î“ Î” Î Î› Î©} (Ï : Î“ ,, Î” â‡’Ê³ Î©)
             â†’ (rename-unassoc {Î” = Î} {Î = Î›}) âˆ˜Ê³ (â‡‘Ê³  (â‡‘Ê³ Ï)) â‰¡Ê³ (â‡‘Ê³ Ï) âˆ˜Ê³ rename-unassoc
  extendÊ³Â² Ï (var-inl (var-inl x)) = refl
  extendÊ³Â² Ï (var-inl (var-inr x)) = refl
  extendÊ³Â² Ï (var-inr x) = refl
