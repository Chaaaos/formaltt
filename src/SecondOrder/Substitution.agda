-- {-# OPTIONS --allow-unsolved-metas #-}

open import Agda.Primitive using (lzero; lsuc; _âŠ”_; Level)
open import Relation.Unary hiding (_âˆˆ_)
open import Data.Empty.Polymorphic
open import Data.List
open import Function.Base
open import Relation.Binary using (Setoid)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; subst)
open import SecondOrder.Arity

import SecondOrder.SecondOrderSignature as SecondOrderSignature

module SecondOrder.Substitution {â„“s â„“o â„“a : Level} {ğ”¸ : Arity} {Î£ : SecondOrderSignature.Signature {â„“s} {â„“o} {â„“a} ğ”¸}where

  open SecondOrderSignature {â„“s} {â„“o} {â„“a}
  open Signature ğ”¸ Î£

  module _ {Î˜ : MetaContext}  where
      infix 4 _â‡’r_

    -- ** Renamings **

      -- renaming
      _â‡’r_ : âˆ€ (Î“ Î” : Context) â†’ Set â„“s
      Î“ â‡’r Î” = âˆ€ {A} â†’ A âˆˆ Î“ â†’ A âˆˆ Î”

      -- extending a renaming
      extend-r : âˆ€ {Î“ Î”} â†’ Î“ â‡’r Î” â†’ âˆ€ {Î} â†’ Î“ ,, Î â‡’r Î” ,, Î
      extend-r Ï (var-inl x) = var-inl (Ï x)
      extend-r Ï (var-inr x) = var-inr x

      -- the identity renaming
      id-r : âˆ€ {Î“ : Context} â†’ Î“ â‡’r Î“
      id-r x = x

      -- composition of renamings
      _âˆ˜r_ : âˆ€ {Î“ Î” Î˜ : Context} â†’ Î” â‡’r Î˜ â†’ Î“ â‡’r Î” â†’ Î“ â‡’r Î˜
      (Ïƒ âˆ˜r Ï) x = Ïƒ (Ï x)

      infix 7 _âˆ˜r_

      -- action of a renaming on terms
      tm-rename : âˆ€ {Î“ Î” A} â†’ Î“ â‡’r Î” â†’ Term Î˜ Î“ A â†’ Term Î˜ Î” A
      tm-rename Ï (tm-var x) = tm-var (Ï x)
      tm-rename Ï (tm-meta M ts) = tm-meta M (Î» i â†’ tm-rename Ï (ts i))
      tm-rename Ï (tm-oper f es) = tm-oper f (Î» i â†’ tm-rename (extend-r Ï) (es i))

      syntax tm-rename Ï t = t [ Ï ]r

      infix 6 tm-rename

      -- the reassociation renaming
      rename-assoc-r : âˆ€ {Î“ Î” Î} â†’ (Î“ ,, Î”) ,, Î â‡’r Î“ ,, (Î” ,, Î)
      rename-assoc-r (var-inl (var-inl x)) = var-inl x
      rename-assoc-r (var-inl (var-inr y)) = var-inr (var-inl y)
      rename-assoc-r (var-inr z) = var-inr (var-inr z)

      rename-assoc-l : âˆ€ {Î“ Î” Î} â†’ Î“ ,, (Î” ,, Î) â‡’r (Î“ ,, Î”) ,, Î
      rename-assoc-l (var-inl x) = var-inl (var-inl x)
      rename-assoc-l (var-inr (var-inl y)) = var-inl (var-inr y)
      rename-assoc-l (var-inr (var-inr z)) = var-inr z

      -- the empty context is the unit
      rename-ctx-empty-r : âˆ€ {Î“} â†’ Î“ ,, ctx-empty â‡’r Î“
      rename-ctx-empty-r (var-inl x) = x

      -- weakening
      weakenË¡ : âˆ€ {Î“ Î” A} â†’ Term Î˜ Î“ A â†’ Term Î˜ (Î“ ,, Î”) A
      weakenË¡ = tm-rename var-inl

      weakenÊ³ : âˆ€ {Î“ Î” A} â†’ Term Î˜ Î” A â†’ Term Î˜ (Î“ ,, Î”) A
      weakenÊ³ = tm-rename var-inr


    -- ** Substitutions **

      -- substitition
      _â‡’s_ : âˆ€ (Î“ Î” : Context) â†’ Set (lsuc (â„“s âŠ” â„“o âŠ” â„“a))
      Î“ â‡’s Î” = âˆ€ {A} (x : A âˆˆ Î”) â†’ Term Î˜ Î“ A

      infix 4 _â‡’s_

      -- extending a substitution
      extend-sË¡ : âˆ€ {Î“ Î” Î} â†’ Î“ â‡’s Î” â†’ Î“ ,, Î â‡’s Î” ,, Î
      extend-sË¡ {Î = Î} Ïƒ (var-inl x) = weakenË¡ (Ïƒ x)
      extend-sË¡ Ïƒ (var-inr x) = tm-var (var-inr x)

      extend-sÊ³ : âˆ€ {Î“ Î” Î} â†’ Î“ â‡’s Î” â†’ Î ,, Î“ â‡’s Î ,, Î”
      extend-sÊ³ {Î = Î} Ïƒ (var-inl x) = tm-var (var-inl x)
      extend-sÊ³ Ïƒ (var-inr x) = weakenÊ³ (Ïƒ x)

      -- the action of a substitution on a term (contravariant)
      _[_]s : âˆ€ {Î“ Î” : Context} {A : sort} â†’ Term Î˜ Î” A â†’ Î“ â‡’s Î” â†’ Term Î˜ Î“ A
      (tm-var x) [ Ïƒ ]s = Ïƒ x
      (tm-meta M ts) [ Ïƒ ]s = tm-meta M (Î» i â†’ (ts i) [ Ïƒ ]s)
      (tm-oper f es) [ Ïƒ ]s = tm-oper f (Î» i â†’ es i [ extend-sË¡ Ïƒ ]s)

      infixr 6 _[_]s

      -- identity substitution
      id-s : âˆ€ {Î“ : Context} â†’ Î“ â‡’s Î“
      id-s = tm-var

      -- composition of substitutions
      _âˆ˜s_ : âˆ€ {Î“ Î” Î˜ : Context} â†’ Î” â‡’s Î˜ â†’ Î“ â‡’s Î” â†’ Î“ â‡’s Î˜
      (Ïƒ âˆ˜s Ï) x = Ïƒ x [ Ï ]s

      infixl 7 _âˆ˜s_



  -- ** Metavariable instantiations **

  -- metavariable instantiation
  mv-inst  : MetaContext â†’ MetaContext â†’ Context â†’ Set (lsuc (â„“s âŠ” â„“o âŠ” â„“a))
  mv-inst Î˜ Î¨ Î“ = âˆ€ (M : mv Î˜) â†’ Term Î¨ (Î“ ,, mv-arity Î˜ M) (mv-sort Î˜ M)

  syntax mv-inst Î˜ Ïˆ Î“ = Ïˆ â‡’M Î˜ âŠ• Î“

  -- action of a metavariable instantiation on terms
  _[_]M : âˆ€ {Î“ : Context} {A : sort} {Î˜ Î¨ : MetaContext} {Î”} â†’ Term Î˜ Î“ A â†’ âˆ€ (Î¹ : mv-inst Î˜ Î¨ Î”) â†’ Term Î¨ (Î” ,, Î“) A
  (tm-var x) [ Î¹ ]M = tm-var (var-inr x)
  _[_]M {Î“ = Î“} {Î˜ = Î˜} {Î” = Î”} (tm-meta M ts) Î¹ = (Î¹ M) [ Ïƒ ]s
    where Ïƒ : Î” ,, Î“ â‡’s Î” ,, mv-arity Î˜ M
          Ïƒ (var-inl x) = tm-var (var-inl x)
          Ïƒ (var-inr x) =  (ts x) [ Î¹ ]M
  _[_]M {Î¨ = Î¨} (tm-oper f es) Î¹ = tm-oper f (Î» i â†’ tm-rename (rename-assoc-l {Î˜ = Î¨}) (es i [ Î¹ ]M) )

  infixr 6 _[_]M

  -- the identity metavariable instantiation
  id-M : âˆ€ {Î˜} â†’ mv-inst Î˜ Î˜ ctx-empty
  id-M t = tm-meta t (Î» i â†’ weakenÊ³ (tm-var i))

  term-reassoc : âˆ€ {Î© Î” Î“ Î A} â†’ Term Î© (ctx-concat Î” (ctx-concat Î“ Î)) A â†’  Term Î© (ctx-concat (ctx-concat Î” Î“) Î) A
  term-reassoc {Î© = Î©} = tm-rename (rename-assoc-l {Î˜ = Î©})


  -- composition of metavariable instantiations
  _âˆ˜M_ : âˆ€ {Î˜ Ïˆ Î© Î“ Î”} â†’ Î© â‡’M Ïˆ âŠ• Î” â†’ Ïˆ â‡’M Î˜ âŠ• Î“ â†’ (Î© â‡’M Î˜ âŠ• (Î” ,, Î“))
  _âˆ˜M_ {Î˜ = Î˜} {Ïˆ = Ïˆ} {Î“ = Î“} {Î” = Î”} Î¼ Î¹ = Î» M â†’ term-reassoc (Î¹ M [ Î¼ ]M)

-- ** Interactions **

  -- action of a metavariable instantiation on a substitution
  _Mâˆ˜s_ : âˆ€ {Î˜ Ïˆ Î“ Î” Î} â†’ Ïˆ â‡’M Î˜ âŠ• Î â†’ _â‡’s_ {Î˜ = Î˜} Î” Î“ â†’ _â‡’s_ {Î˜ = Ïˆ} (Î ,, Î”) (Î ,, Î“)
  (Î¹ Mâˆ˜s Ïƒ) (var-inl x) = tm-var (var-inl x)
  (Î¹ Mâˆ˜s Ïƒ) (var-inr x) = Ïƒ x [ Î¹ ]M

  -- action of a substitution on a metavariable instantiation
  _sâˆ˜M_ : âˆ€ {Î˜ Ïˆ Î“ Î”} â†’ _â‡’s_ {Î˜ = Ïˆ} Î” Î“ â†’ Ïˆ â‡’M Î˜ âŠ• Î“ â†’ Ïˆ â‡’M Î˜ âŠ• Î”
  _sâˆ˜M_ Ïƒ Î¹ M = Î¹ M [ extend-sË¡ Ïƒ ]s
